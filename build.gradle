buildscript {
    ext {
        /************* 開発環境 *************/
        dbUrl = 'jdbc:mysql://lemonico-db.crwkn7mkbqkv.ap-northeast-1.rds.amazonaws.com:3306/lemonico?useSSL=false&useInformationSchema=true'
        dbUser = 'root'
        dbPassword = 'FVReP1cMZM7rQ1dchi1K'
        /************* 本番環境 *************/
//        dbUrl = 'jdbc:mysql://lemonico.crwkn7mkbqkv.ap-northeast-1.rds.amazonaws.com:3306/lemonico?useSSL=false&useInformationSchema=true'
//        dbUser = 'admin'
//        dbPassword = 'password'
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.5.5"
        classpath "gradle.plugin.com.boxfuse.client:gradle-plugin-publishing:6.0.6"
        classpath "com.diffplug.spotless:spotless-plugin-gradle:5.16.0"
    }
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id "org.flywaydb.flyway" version "5.0.7"
}
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.flywaydb.flyway'
apply plugin: "com.diffplug.spotless"

//idea.module.inheritOutputDirs = true

group 'lemoico.com'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

configurations {
    domaGenRuntime
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.seasar.doma:doma-processor:2.50.0'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.6.1'

    compileOnly 'org.projectlombok:lombok:1.18.22'

    implementation 'org.springframework.boot:spring-boot-starter-web:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-logging:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-aop:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-security:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client:2.6.1'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server:2.6.1'

    // json web token
    implementation 'io.jsonwebtoken:jjwt:0.9.1'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'

    // aop関連
    implementation 'org.aspectj:aspectjweaver:1.9.7'

    // DBアクセス関連
    implementation 'org.seasar.doma:doma-core:2.50.0'
    implementation 'org.seasar.doma.boot:doma-spring-boot-starter:1.5.0'
    implementation 'mysql:mysql-connector-java:5.1.49'
    implementation 'com.h2database:h2:2.0.202'

    domaGenRuntime 'mysql:mysql-connector-java:5.1.49'
    domaGenRuntime 'org.seasar.doma:doma-gen:2.28.0'

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation 'junit:junit:4.13.2'

    // AWS JAVA SDK For Amazon S3
    implementation 'software.amazon.awssdk:s3:2.17.99'

    // AWS JAVA SDK For Amazon Rekognition
    implementation 'software.amazon.awssdk:rekognition:2.17.91'
}

sourceSets {
    main {
        java {
            srcDirs 'src/generated/main/java'
            srcDirs 'src/main/java'
        }
        resources {
            srcDirs 'src/generated/main/resources'
            srcDirs 'src/main/resources'
        }
    }
}

// 【DOMA4019】のため
task copyDomaResources(type: Sync)  {
    from sourceSets.main.resources.srcDirs
    into compileJava.destinationDir
    include 'doma.compile.config'
    include 'META-INF/**/*.sql'
    include 'META-INF/**/*.script'
}

compileJava {
    // Depend on the above task
    dependsOn copyDomaResources
    options.encoding = 'UTF-8'
}

task genRepository {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties', classpath: configurations.domaGenRuntime.asPath)
        ant.gen(url: "${dbUrl}",
                user: "${dbUser}",
                password: "${dbPassword}",
                ignoredTableNamePattern: "flyway_schema_history",
                templatePrimaryDir: "doma/repository")
                {
                    entityConfig(
                            overwrite: true, useListener: false,
                            packageName: "api.lemonico.repository",
                            entitySuffix: "Repository",
                            destDir: 'src/main/java')
                }
    }
}

task genResource {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties', classpath: configurations.domaGenRuntime.asPath)
        ant.gen(url: "${dbUrl}",
                user: "${dbUser}",
                password: "${dbPassword}",
                ignoredTableNamePattern: "flyway_schema_history",
                templatePrimaryDir: "doma/resource")
                {
                    entityConfig(
                            overwrite: true, useListener: false,
                            packageName: "api.lemonico.resource",
                            entitySuffix: "Resource",
                            destDir: 'src/main/java')
                }
    }
}

task genEntity {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties', classpath: configurations.domaGenRuntime.asPath)
        ant.gen(
                url: "${dbUrl}",
                user: "${dbUser}",
                password: "${dbPassword}",
                ignoredTableNamePattern: "flyway_schema_history",
                templatePrimaryDir: "doma/entity")
                {
                    entityConfig(
                            overwrite: true,
                            useListener: false,
//                            superclassName: 'api.lemonico.entity.LcEntity',
                            packageName: "api.lemonico.entity",
                            destDir: 'src/main/java'
                    )
                }
    }
}

task genDao {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties', classpath: configurations.domaGenRuntime.asPath)
        ant.gen(
                url: "${dbUrl}",
                user: "${dbUser}",
                password: "${dbPassword}",
                ignoredTableNamePattern: "flyway_schema_history",
                templatePrimaryDir: "doma/dao")
                {
                    daoConfig(
                            overwrite: true,
                            packageName: "api.lemonico.dao",
                            suffix: "Dao",
                            destDir: 'src/main/java')
                    sqlConfig(
                            generate: true,
                            overwrite: true,
                            destDir: 'src/main/resources',
                    )
                }
    }
}

task genTestCases {
    group = 'doma-gen'
    doLast {
        ant.taskdef(resource: 'domagentask.properties',
                classpath: configurations.domaGenRuntime.asPath)
        ant.gen(url: "${dbUrl}", user: "${dbUser}", password: "${dbPassword}") {
            sqlTestCaseConfig {
                fileset(dir: 'src/main/resources') {
                    include(name: 'META-INF/**/*.sql')
                }
            }
        }
    }
}

// JavaクラスとSQLファイルの出力先ディレクトリを同じにする
processResources.destinationDir = compileJava.destinationDir
// コンパイルより前にSQLファイルを出力先ディレクトリにコピーするために依存関係を逆転する
compileJava.dependsOn processResources

flyway {
    url = "${dbUrl}"
    user = "${dbUser}"
    password = "${dbPassword}"
    locations = ["filesystem:$projectDir/db/migration"]
}

spotless {
    java {
        importOrder()
        eclipse().configFile file("${rootDir}/eclipse-java-code-formatter.xml")
        removeUnusedImports()
    }
}

//デプロイ用JARファイル作成してから、releaseフォルダにコピーする
task copyJarToRelease(type: Copy, dependsOn: bootJar) {
    group = 'build'
    from 'build/libs'
    into 'release'
    include 'lemonico-api-1.0-SNAPSHOT.jar'
}
